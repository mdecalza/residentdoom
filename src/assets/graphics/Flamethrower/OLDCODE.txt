Actor UTNTFlamethrower : Weapon
{
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "You got the flamethrower! Time for a BBQ!"
  Obituary "%o was BBQed by %k's flamethrower."
  Weapon.AmmoType "Gas"
  Weapon.AmmoGive 48
  Weapon.AmmoUse 1
  Weapon.SlotNumber 8
  +Weapon.Explosive
  States
  {
  Spawn:
    WFLM A -1
    Loop
  Ready:
    FLMG BCD 3 A_WeaponReady
    Loop
  Deselect:
    FLMG A 1 A_Lower
    Loop
  Select:
    FLMG A 1 A_Raise
    Loop
  Fire:
    FLMG E 0 bright A_GunFlash
    FLMG E 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG F 1 bright A_FireCustomMissile("FTFire", 0, 0, Random(-4, 4), 0)
    FLMG G 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG E 0 bright A_GunFlash
    FLMG E 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG F 1 bright A_FireCustomMissile("FTFire", 0, 0, Random(-4, 4), 0)
    FLMG G 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG E 0 bright A_GunFlash
    FLMG E 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG F 1 bright A_FireCustomMissile("FTFire", 0, 0, Random(-4, 4), 0)
    FLMG G 1 bright A_FireCustomMissile("FTFire", 0, 1, Random(-4, 4), 0)
    FLMG H 2 bright A_ReFire
    Goto Ready
  Flash:
    TNT1 A 2 A_Light1
    TNT1 A 2 A_Light2
    TNT1 A 1 A_Light1
    TNT1 A 1 A_Light0
    Stop
  }
}

Actor FTFire
{
  Radius 2
  Height 4
  Speed 16
  Damage 1
  Projectile
  RenderStyle Add
  DamageType Fire
  Alpha 0.67
  Scale 0.67
  SeeSound "Flamethrower/Fire"
  DeathSound "Flamethrower/Hit"
  States
  {
  Spawn:
    TNT1 A 2
    FRFX ABCD 2 bright A_Explode(5, 8, 0)
    TNT1 A 0 A_LowGravity
    FRFX EFG 2 bright A_Explode(5, 16, 0)
    FRFX HIJ 2 bright A_Explode(5, 32, 0)
    FRFX KLM 2 bright A_Explode(5, 64, 0)
    FRFX NO 2 bright
    Stop
  Death:
    FRFX HIJ 2 bright A_Explode(5, 32, 0)
    TNT1 A 0 A_CustomMissile("DropFireLA", 0, 0, 0, 4)
    FRFX KLM 2 bright A_Explode(5, 64, 0)
    FRFX NO 2 bright
    Stop
  }
}

Actor DropFireLA //limited audio
{
Projectile
Radius 8
Height 40
RenderStyle Add
DamageType Fire
Alpha 0.90
-NOGRAVITY
Gravity 0.125
+NODAMAGETHRUST
-MISSILE
States
	{
	Spawn:
		FLME A 1 Bright NoDelay A_Playsound("Flamethrower/Flame/Limited",7,0.4,true)
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("Flamethrower/Flame/Limited",7,0.4,true)
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("Flamethrower/Flame/Limited",7,0.4,true)
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("Flamethrower/Flame/Limited",7,0.4,true)
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("Flamethrower/Flame/Limited",7,0.4,true)
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		TNT1 A 0 A_Jump(192,"Death")
		Loop
	Death:
		FLME A 2 Bright A_SetScale(0.8)
		FLME B 2 Bright A_SetScale(0.6)
		FLME C 2 Bright A_SetScale(0.4)
		FLME D 2 Bright A_SetScale(0.2)
		TNT1 A 1 A_StopSound(7)
		Stop
	}
}

Actor Gas : Ammo
{
  Inventory.PickupSound "misc/pickup"
  Inventory.Amount 24
  Inventory.MaxAmount 240
  Ammo.BackpackAmount 24
  Ammo.BackpackMaxAmount 480
  Inventory.PickupMessage "You picked up a gasoline cannister"
  Inventory.Icon AGASA0
  States
  {
  Spawn:
    AGAS A -1
    Loop
  }
}

Actor BigGas : Gas
{
  Inventory.Amount 120
  Inventory.PickupMessage "You picked up a gasoline pack"
  Inventory.Icon AGASB0
  States
  {
  Spawn:
    AGAS B -1
    Loop
  }
}