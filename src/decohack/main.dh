/*****************************************************************************
 * DECOHack Patch File
 * Main
 * Matthew DeCalzadilla
 ****************************************************************************/

#include <mbf21>
#include <friendly>

misc
{
	initialBullets 10
	initialHealth 50
}

// Bullets
ammo 0
{
	max 30
}

// Shells
ammo 1
{
	max 18
}

// Cells
ammo 2
{
	max 120
}

// Rockets
ammo 3
{
	max 15
}

// Make all hanging bodies non-solid
each thing from 129 to 134
{
	-SOLID
}

// Make all melee weapons silent
each weapon in (0,4,7)
{
	+SILENT
}

// Remove auto-switching on ammo pickup
each weapon from 0 to 1
{
	-AUTOSWITCHFROM
}

// Rename Rocket pickup for map editor convenience
thing 66 "Grenade" {}

// Rename Box of rockets pickup for map editor convenience
thing 67 "Box of grenades" {}

// Rename Chainsaw pickup for map editor convenience
thing 75 "Axe" {}

// Rename Rocket launcher pickup for map editor convenience
thing 76 "Hand grenades" {}

strings
{
	GOTARMOR "This armor should keep me safe."
	GOTMEGA "Now this is some heavy-duty armor!"
	GOTHTHBONUS "This strange potion numbs the pain a little..."
	GOTARMBONUS "I can repair my armor with this scrap."
	GOTSTIM "A first aid kit!"
	GOTMEDINEED "Thought I was a goner for sure..."
	GOTMEDIKIT "A surgical kit!"
	GOTSUPER "Woah! I feel younger!"
	GOTBLUECARD "A blue keycard."
	GOTYELWCARD "A yellow keycard."
	GOTREDCARD "A red keycard."
	GOTBLUESKUL "A glowing blue skull."
	GOTYELWSKUL "A glowing yellow skull."
	GOTREDSKULL "A glowing red skull."
	GOTINVUL "I feel invulnerable!"
	GOTBERSERK "I feel like I can punch through the walls!!"
	GOTINVIS "I'm invisible!"
	GOTSUIT "This hazmat suit will protect me."
	GOTMAP "The blueprint of the house!"
	GOTVISOR "Nightvision goggles. Let's hope the battery's not low..."
	GOTMSPHERE "I've never felt better!"
	GOTCLIP "More bullets for my pistol."
	GOTCLIPBOX "Wow. That's a lot of bullets."
	GOTROCKET "A grenade."
	GOTROCKBOX "A box of hand grenades!"
	GOTCELL "Some fuel."
	GOTCELLBOX "This is a lot of fuel!"
	GOTSHELLS "Shotgun shells."
	GOTSHELLBOX "A box of shotgun shells!"
	GOTBACKPACK "I can carry some more ammo in this backpack."
	GOTBFG9000 "What on Earth is this!?"
	GOTCHAINGUN "An assault rifle!"
	GOTCHAINSAW "An axe!"
	GOTLAUNCHER "Hand grenades!"
	GOTPLASMA "A flamethrower!?"
	GOTSHOTGUN "A shotgun!"
	GOTSHOTGUN2 "Nice! A double-barreled shotgun!"

	PD_BLUEO "I need a blue key to activate this."
	PD_REDO "I need a red key to activate this."
	PD_YELLOWO "I need a yellow key to activate this."
	PD_BLUEK "I need a blue key to open this door."
	PD_REDK "I need a red key to open this door."
	PD_YELLOWK "I need a yellow key to open this door."
	PD_BLUEC "I need a blue keycard to open this door."
	PD_REDC "I need a red keycard to open this door."
	PD_YELLOWC "I need a yellow keycard to open this door."
	PD_BLUES "I need a blue skull to open this door."
	PD_REDS "I need a red skull to open this door."
	PD_YELLOWS "I need a yellow skull to open this door."
	PD_ANY "I suppose any key could open this door."
	PD_ALL3 "This door needs three keys to open."
	PD_ALL6 "Looks like I need six keys to open this door..."

	STSTR_CHOPPERS "And my axe!"
}

// Fist
weapon 0
{
	States
	{
		Fire:
			PUNG C 1
			PUNG D 5 A_Punch
			PUNG CBA 5
			Goto Ready
	}
}

// Pistol
weapon 1
{
	States
	{
		Fire:
			PISG A 4
			PISG B 1 Bright A_Light2
			PISG B 0 A_WeaponSound("PISTOL")
			PISG B 0 A_ConsumeAmmo
			PISG B 0 A_WeaponBulletAttack(3.0,3.0,1,12,2)
			PISG F 5 A_Light0
			PISG D 9
			PISG A 4 A_ReFire
			Goto Ready
	}
}

// Pump-action Shotgun
weapon 2
{
	States
	{
		Fire:
			SHTG B 1 Bright A_Light2
			SHTG A 0 A_WeaponSound("SHOTGN")
			SHTG A 0 A_ConsumeAmmo
			SHTG A 0 A_WeaponBulletAttack(3.0,3.0,7,8,3)
			SHTG D 2 A_Light0
			SHTG CAC 7
			SHTG D 7
			SHTG E 8 A_WeaponSound ("FRE000")
			SHTG D 9
			SHTG C 8
			SHTG A 4
			SHTG A 4 A_ReFire
			Goto Ready
	}
}

// An actual grenade which will bounce around the map before detonating.
thing 151 : thing 34 "Grenade in flight"
{
	Health 5
	Mass 256

	+SHOOTABLE
	-NOBLOCKMAP
	-NOGRAVITY
	-MISSILE //—does not replicate behavior from hovercab.wad
	+BOUNCES

	States
	{
		Spawn:
			SP00 ABCDEF 3
			SP00 ABCDEF 4
			SP00 ABCDEF 6
			SP00 ABC 10
			SP00 C 10
			Goto Death
		Death:
			MISL B 0 Bright A_PlaySound("BAREXP")
			MISL B 0 Bright A_NoiseAlert
			MISL B 8 Bright A_RadiusDamage(128,256)
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}

// Exists to get around a DECOHack limitation on A_WeaponProjectile
// A_WeaponProjectile can only spawn things flagged with MISSILE
// which prevents the grenade from bouncing around before exploding.
thing 152 : thing 34 "Magic grenade spawner"
{
	States
	{
		Spawn:
			SP00 A 3
			SP00 A 0 A_SpawnObject(151,-90.0,0.0,0.0,0.0,-0.5,25.0,7.5)
			Stop
	}
}

// Spawns a Magic grenade spawner which spawns a real grenade.
// Gets around the DECOHack limitation specified above Magic grenade spawner.
weapon 4 "Hand grenade"
{
	States
	{
		Fire:
			MISG BC 2
			MISG D 6
			MISG E 12 A_WeaponSound("FRE001")
			MISG E 1 A_RefireTo(FireHold,0)
			Goto Throw
		Throw:
			MISG F 2 A_WeaponProjectile(152,0.0,0.0,10.0,5.0)
			MISG F 0 A_ConsumeAmmo
			MISG F 0
			MISG G 16 A_WeaponSound("SKESWG")
			Goto Ready
		FireHold:
			MISG E 1
			MISG E 1 A_RefireTo(FireHold,0)
			Goto Throw
	}
}

weapon 7 "Axe"
{
	-NOTHRUST

	States
	{
		Ready:
			SAWG A 1 A_WeaponReady
			Loop
		Deselect:
			SAWG A 1 A_Lower
			Loop
		Select:
			SAWG A 1 A_Raise
			Loop
		Fire:
			SAWG C 8
			SAWG B 12
			SAWG C 1 A_WeaponSound("SKESWG")
			SAWG D 0 A_WeaponMeleeAttack(10,3,10.0, "SAWHIT",92.0)
			SAWG D 3 A_WeaponMeleeAttack(10,3,10.0, "SAWHIT",92.0)
			SAWG E 8
			SAWG D 8
			Goto Ready
	}
}

thing 2 "Former Human"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			POSS H 5
			POSS I 5 A_Scream
			POSS J 5 A_Fall
			POSS K 5
			POSS L 150
			POSS K 5 A_PlaySound("SLOP")
			POSS JIH 5
			POSS H 0 A_SpawnObject(2,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
		Raise:
			SPOS TSRQPONM 5
			Goto See
	}
}

thing 3 "Former Sergeant"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_Fall
			SPOS K 5
			SPOS L 175
			SPOS K 5 A_PlaySound("SLOP")
			SPOS JIH 5
			SPOS H 0 A_SpawnObject(3,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
		Raise:
			SPOS TSRQPONM 5
			Goto See
	}
}

// Revenant
thing 6
{
	States
	{
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_Fall
			SKEL P 7
			SKEL Q 875
			SKEL Q 5 A_PlaySound("SLOP")
			SKEL PONML 5
			SKEL L 0 A_SpawnObject(6,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
	}
}

thing 11 "Chaingunner"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_Fall
			CPOS KLM 5
			CPOS N 250
			CPOS N 5 A_PlaySound("SLOP")
			CPOS MLKJIH 5
			CPOS H 0 A_SpawnObject(11,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
	}
}

// Imp
thing 12
{
	painchance 256

	States
	{
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_Fall
			TROO M 150
			TROO M 8 A_PlaySound("SLOP")
			TROO L 8
			TROO KJI 6
			TROO I 0 A_SpawnObject(12,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
	}
}

// Demon
thing 13
{
	States
	{
		Death:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_Fall
			SARG M 4
			SARG N 300
			SARG N 5 A_PlaySound("SLOP")
			SARG MLKJI 5
			SARG I 0 A_SpawnObject(13,0.0,0.0,0.0,0.0,0.0,0.0,0.0)
			Stop
	}
}